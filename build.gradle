import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version "${SpringBootVersion}"
    id 'io.spring.dependency-management' version "${DependencyManagement}"
    id 'org.jreleaser' version "${Jreleaser}"
}

apply from: 'version.gradle'

allprojects {
    group "${group}"
    version "${version}"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    compileJava.options.compilerArgs.addAll(['-parameters'])

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    if (project.name.indexOf('example') >= 0) {
        return
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = "${group}"
                artifactId = project.name
                version = "${version}"

                from(components.java)

                pom {
                    name = project.name
                    description = "lightweight edge data acquisition"
                    url = "https://github.com/vbeats/vline-j"
                    licenses {
                        license {
                            name = "MIT"
                            url = "https://github.com/vbeats/vline-j/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "bootvue"
                            name = "bootvue"
                            email = "bootvue@gmail.com"
                        }
                    }
                    scm {
                        connection = "https://github.com/vbeats"
                        developerConnection = "https://github.com/vbeats"
                        url = "https://github.com/vbeats"
                    }
                }
            }
        }

        repositories {
            maven {
                url = rootProject.layout.buildDirectory.dir('publish')
            }
        }
    }

    jreleaser {
        project {
            inceptionYear = "2025"
            description = "lightweight edge data acquisition"
            copyright = "vbeats"
        }

        signing {
            active = 'ALWAYS'
            armored = true
        }

        release {
            github {
                skipRelease = true
                skipTag = true
            }
        }

        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        sign = true
                        sourceJar = true
                        javadocJar = true
                        verifyPom = true
                        applyMavenCentralRules = false
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository('build/publish')
                    }
                }
            }
        }
    }
}

publish {
    doFirst {
        mkdir 'build/publish'
        mkdir 'build/jreleaser'
    }
}
